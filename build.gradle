apply plugin: 'java'
apply plugin: 'osgi'

project.group   = 'org.obrel'
project.version = '1.1.0'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
	// enable compilation of internal sun classes
	// see http://stackoverflow.com/questions/12554829/
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}

// disable Java 8 doclint for Javadoc
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}
tasks.withType(Javadoc) {
    failOnError = false
    options.addStringOption('XDignore.symbol.file', '-quiet')
}

repositories
{
    jcenter()
}

dependencies
{
	if (project(':esoco-common').getProjectDir().exists())
	{
		compile project(':esoco-common')
	}
	else
	{
		compile 'de.esoco:esoco-common:1.0.+'
	}

    testCompile 'junit:junit:4.+'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}
