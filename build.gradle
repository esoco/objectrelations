buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+' }
}

apply plugin: 'java'
apply plugin: 'eclipse'

project.group   = 'org.obrel'

sourceCompatibility = 1.8

repositories
{
    jcenter()
}

dependencies
{
	if (project(':esoco-common').getProjectDir().exists())
	{
		implementation project(':esoco-common')
	}
	else
	{
		implementation 'de.esoco:esoco-common:1.3.+'
	}
	
	testImplementation(platform("org.junit:junit-bom:5.5.1"))	
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation('junit:junit:4.12')
	testRuntimeOnly('org.junit.vintage:junit-vintage-engine:5.4.0') // JUnit4
}

test {
	useJUnitPlatform()
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

tasks.withType(Javadoc) {
    failOnError = false
    options.addBooleanOption('quiet', true)
    
    if (JavaVersion.current().isJava8Compatible()) {
        options.addBooleanOption('Xdoclint:none', true) 
    }
}

apply from: 'publish.gradle'